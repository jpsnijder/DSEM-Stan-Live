TITLE: Model 2 WCP
DATA: FILE = "mplus/mplus.dat";

VARIABLE:
    NAMES = id day X Y;  ! variables names (in the order they appear in the data file) 
    USEVARIABLES = X Y;      ! which variables to include in the analysis
    CLUSTER = id;            ! which variable indicates the clustering of the data
    LAGGED = X(1) Y(1);      ! create lagged versions of X and Y (lag 1)
    TINTERVAL = day(1);      ! which variable indicates the timing of observations
    MISSING = ALL (-999);

SAVEDATA:
    BPARAMETER = draws_m2.dat;

ANALYSIS:   TYPE = TWOLEVEL RANDOM;
              ESTIMATOR = BAYES;
              ALGORITHM=GIBBS(PX1);
              PROC=16;
              CHAINS=4;
              BITER = (3500); 
              BSEED = 0222;
              THIN = 5;        

MODEL:
  %WITHIN%
phi_X   | X ON X&1; ! autoregression X (lag 1)
phi_Y   | Y ON Y&1; ! autoregression Y (lag 1)
phi_XY  | Y on X&1; ! cross-lagged regression from X to Y (lag 1)
beta_YX | X ON Y;   ! cross-regression from Y to X (lag 0)
psi_X   | X;        ! random residual variance X
psi_Y   | Y;        ! random residual variance Y


  %BETWEEN%
  ! allow all random effects to be correlated
   X Y phi_X-psi_Y WITH Y phi_X-psi_Y;

OUTPUT: TECH1 TECH8 STDYX FSCOMPARISON STAND(CLUSTER);
  
